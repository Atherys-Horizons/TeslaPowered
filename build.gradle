plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group 'com.mcsimonflash.sponge.teslapowered'
version '1.1.1'
ext.spongeversion = '7.1.0-SNAPSHOT'
version "s${spongeversion[0..2]}-v${version}"

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.spongepowered.plugin'
    dependencies {
        compile "org.spongepowered:spongeapi:${spongeversion}"
    }

    version = rootProject.version

    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        classifier = "sources"
    }
    artifacts.archives sourcesJar
}

dependencies {
    compile project(":TeslaCore")
    compile project(":TeslaLibs")
}

shadowJar {
    dependencies {
        include project(":TeslaCore")
        include project(":TeslaLibs")
    }
    classifier = null // removes the '-all' ending
}

task copyJars(type: Copy) {
    from([subprojects.jar, shadowJar])
    into project.file("#releases/${version}")
}

build.dependsOn(shadowJar, copyJars)